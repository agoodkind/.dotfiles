
# shellcheck shell=bash
# Check if profiling was requested and load zprof module early if needed
SHOULD_PROFILE=false
if [[ -f ~/.cache/zsh_profile_next ]]; then
    SHOULD_PROFILE=true
    zmodload zsh/zprof
    rm ~/.cache/zsh_profile_next

    do_profile() {
        echo "Zsh performance profiling results:"
        zprof
    }
fi
export SHOULD_PROFILE

# Run this function to enable profiling for the next shell session
zsh_profile() {
    mkdir -p ~/.cache
    touch ~/.cache/zsh_profile_next
    echo "Performance profiling enabled for next shell session"
}

# store timestamp for debugging
export DOTDOTFILES_TIMESTAMP="$(date '+%Y-%m-%d %H:%M:%S')"

# Cache OS detection
if [[ ! -f ~/.cache/os-type.cache ]]; then
    echo "Detecting & caching OS type"
    mkdir -p ~/.cache
    if [[ $(uname) == "Darwin" ]]; then
        echo "mac" > ~/.cache/os-type.cache
    elif command -v apt >/dev/null; then
        echo "ubuntu" > ~/.cache/os-type.cache
    else
        echo "unknown" > ~/.cache/os-type.cache
    fi
fi

OS_TYPE=$(cat ~/.cache/os-type.cache)

case "$OS_TYPE" in
    mac)
        source "$DOTDOTFILES/lib/include/.zshrc.mac"
        ;;
    ubuntu)
        source "$DOTDOTFILES/lib/include/.zshrc.ubuntu"
        ;;
    *)
        echo 'Unknown OS!'
        ;;
esac

function is_macos() {
    [[ "$OS_TYPE" == "mac" ]]
}

function is_ubuntu() {
    [[ "$OS_TYPE" == "ubuntu" ]]
}

### Zinit
source "$DOTDOTFILES/lib/zinit/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# zoxide is fancy cd
source "$DOTDOTFILES/lib/include/.zshrc.zoxide"

# Load local zshrc customizations that are not to be tracked by git
[[ ! -f "$DOTDOTFILES/.zshrc.local" ]] || source "$DOTDOTFILES/.zshrc.local"

# the fuck - lazy load this function
fuck () {
    # Undefine this function and source the real one on first use
    unfunction fuck
    eval "$(thefuck --alias)"
    fuck "$@"
}

function reload {
    echo "Reloading zshrc..."
    local _timestamp="$DOTDOTFILES_TIMESTAMP"
    # Preserve current directory
    local _pwd=$PWD
    # Refresh command hash table (new installs)
    rehash
    # Recompute dircolors cache if needed (kept in sync with original logic)
    if [[ ! -f ~/.cache/dircolors.cache ]] || [[ ~/.dir_colors -nt ~/.cache/dircolors.cache ]]; then
        mkdir -p ~/.cache
        dircolors -b > ~/.cache/dircolors.cache
    fi
    # Source main config (will redefine this function)
    source "$HOME/.zshrc"
    # Sync history with other sessions
    fc -RI
    # Return to original directory
    builtin cd "$_pwd"
    if [[ "$_timestamp" != "$DOTDOTFILES_TIMESTAMP" ]]; then
        echo "zshrc reloaded."
    else
        echo "zshrc was unable to be reloaded."
    fi
}

function repair {
    echo "Repairing dotfiles..."
    config checkout main
    config pull
    config submodule update --init --recursive --remote
    "$DOTDOTFILES/repair.sh"
    reload
}

# dotfile management
function config {
    case $1 in
        update)
            git --git-dir="$DOTDOTFILES/.git" --work-tree="$DOTDOTFILES" pull
            (cd "$DOTDOTFILES" && git submodule update --init --recursive)
            ;;
        reload)
            reload
            ;;
        repair)
            repair
            ;;
        *)
            git --git-dir="$DOTDOTFILES/.git" --work-tree="$DOTDOTFILES" "$@"
            ;;
    esac
}

# Synchronous dotfiles update check
DOTFILES_GIT_HASH_FILE="$HOME/.cache/dotfiles-git-hash"
DOTFILES_GIT_DIR="$DOTDOTFILES/.git"
DOTFILES_GIT_HASH="$(git --git-dir="$DOTFILES_GIT_DIR" rev-parse HEAD 2>/dev/null)"
NEEDS_DOTFILES_UPDATE=false
if [[ ! -f "$DOTFILES_GIT_HASH_FILE" ]]; then
    NEEDS_DOTFILES_UPDATE=true
elif [[ "$DOTFILES_GIT_HASH" != "$(cat "$DOTFILES_GIT_HASH_FILE" 2>/dev/null)" ]]; then
    NEEDS_DOTFILES_UPDATE=true
fi

if $NEEDS_DOTFILES_UPDATE; then
    echo "Dotfiles update needed."
fi
