# shellcheck shell=bash
###############################################################################
# MOTD
MOTD_INTERVAL_HOURS="${MOTD_INTERVAL_HOURS:-12}"
MOTD_CACHE_FILE="$HOME/.cache/motd_last_shown"
MOTD_FORCE_FILE="$HOME/.cache/motd_show_next"

should_show_motd=false

# Force flag (highest priority)
if [[ -f "$MOTD_FORCE_FILE" ]]; then
  should_show_motd=true
  rm -f "$MOTD_FORCE_FILE"
# SSH connections
elif [[ -n "$SSH_CONNECTION$SSH_CLIENT" ]] && [[ ! -f "$HOME/.cache/motd_disable_ssh" ]]; then
  should_show_motd=true
# First run or periodic check
elif [[ ! -f "$MOTD_CACHE_FILE" ]]; then
  mkdir -p "$HOME/.cache"
  should_show_motd=true
else
  # Use zsh builtins for timestamps
  zmodload -F zsh/stat b:zstat 2>/dev/null
  if (( $+builtins[zstat] )); then
    zstat -A last_shown +mtime "$MOTD_CACHE_FILE"
    now=$EPOCHSECONDS
    last_date=${(%):-"%D{%Y-%m-%d}"}
    # Get last shown date using strftime
    strftime -s last_shown_date "%Y-%m-%d" "$last_shown"
    if [[ "$last_shown_date" != "$last_date" ]] || (( (now - last_shown) / 3600 >= MOTD_INTERVAL_HOURS )); then
      should_show_motd=true
    fi
  else
    # Fallback for missing zstat
    should_show_motd=true
  fi
fi

function motd {
  local MOTD_DIR="$DOTDOTFILES/lib/motd"
  [[ -d "$MOTD_DIR" ]] || return 0
  for script in "$MOTD_DIR"/*; do
    [[ -f "$script" && -x "$script" ]] && "$script"
  done
}

# Show login info
LOGININFO_SCRIPT="$DOTDOTFILES/lib/motd/80-logininfo"
[[ -f "$LOGININFO_SCRIPT" && -x "$LOGININFO_SCRIPT" ]] && "$LOGININFO_SCRIPT"

if [[ "$should_show_motd" == "true" ]]; then
  MOTD_DIR="$DOTDOTFILES/lib/motd"
  if [[ -d "$MOTD_DIR" ]]; then
    for script in "$MOTD_DIR"/*; do
      [[ "$script" == "$LOGININFO_SCRIPT" ]] && continue
      [[ -f "$script" && -x "$script" ]] && "$script"
    done
  fi
  touch "$MOTD_CACHE_FILE"
fi
